// --- Improved 'include' ---

include = >{
	function {
		fn = [$0];
		for (i = (if (include.root == void) 0 else -1); {
			if (i >= include.searchPaths.n) throw(err)
			else if (exists(@::included[(path = (if (i < 0) include.root else include.searchPaths[i]) # fn)])) ( false )
			else if ((x = try(>source = load(path))) != void) { err = coalesce(@err, x); ( true ) }
			else {
				if (source{:2} == '#!') source = source{find(source, LF):};
				lastRoot = ::include.root;
				::include.root = path{:rfind(path, '/\:') + 1};
				[$0] = path;
				frame = @^$;
				x = try(>evaluate(source, frame));
				::include.root = lastRoot;
				if (x != void) throw(x);
				::included[path] = true;
				( false )
			} 
		}; ++i)
	}(@$0)
};
include.root = void;
compose(@include.searchPaths, '');
include.addSearchPath = >append(@include.searchPaths, $0 # (if (right($0, 1) != '/') '/'));
if (exists(@::getenv)) tokenize(getenv('PIKAINCLUDE'), include.addSearchPath, ';,');
