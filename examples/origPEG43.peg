# Hierarchical syntax
Grammar		<- SPACING Definition+ ENDOFFILE
Definition	<- (Identifier SPACING) '<-' SPACING Expression

Expression	<- Sequence ('/' SPACING Sequence)*
Sequence	<- Term*
Term		<- Prefix SPACING Primary SPACING Suffix SPACING
Prefix		<- (And / Not)?
Suffix		<- (Question / Star / Plus)?
Primary		<- Identifier SPACING !'<-'
			 / '(' SPACING Expression ')'
			 / Literal / Class / Dot

# Lexical syntax
Identifier	<- [a-zA-Z_] [a-zA-Z_0-9]*

Literal		<- ['] (!['] Char)* [']
			 / ["] (!["] Char)* ["]
Class		<- '[' (!']' Range)* ']'
Range		<- Char '-' Char / Char
Char		<- '\\' [nrt'"\[\]\\]
			 / '\\' [0-2][0-7][0-7]
			 / '\\' [0-7][0-7]?
			 / !'\\' .

And			<- '&'
Not			<- '!'
Question	<- '?'
Star		<- '*'
Plus		<- '+'
Dot			<- '.'

SPACING		<- (SPACE / COMMENT)*
COMMENT		<- '#' (!ENDOFLINE .)* ENDOFLINE
SPACE		<- ' ' / '\t' / ENDOFLINE
ENDOFLINE	<- '\r\n' / '\n' / '\r'
ENDOFFILE	<- !.
