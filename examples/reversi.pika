#! /usr/local/bin/PikaCmd
include('stdlib.pika');

INIT_STATE = '...........................OX......XO...........................;X;0';
// INIT_STATE = '.XXXXXXX..XOOOXX.OOOOXXXOOOOXOOO..OOOOOO...OO.OO....OOOO....O.X.;X;14';

map(@OPPOSITE_COLORS, 'X','O' , 'O','X');
map(@COLOR_SCORES, 'X',1 , 'O',-1 , '.',0);
compose(@SQUARE_SCORES
		, 640,   2,  17,  73,  73,  17,   2, 640
		,   2,   2,   4,   1,   1,   4,   2,   2
		,  17,   4,   3,   3,   3,   3,   4,  17
		,  73,   1,   3,   1,   1,   3,   1,  73
		,  73,   1,   3,   1,   1,   3,   1,  73
		,  17,   4,   3,   3,   3,   3,   4,  17
		,   2,   2,   4,   1,   1,   4,   2,   2
		, 640,   2,  17,  73,  73,  17,   2, 640);

onBoard = function /* (x, y) */ { (0 <= $0 && $0 < 8 && 0 <= $1 && $1 < 8) };
getTurnColor = function /* (state) */ { $0{65} };
getSquare = function /* (state, x, y) */ { $0{$2 * 8 + $1} };
getScore = function /* (state) */ { $0{67:} * COLOR_SCORES[getTurnColor($0)] };
squareScore = function /* (x:$0, y:$1, color:$2) */ { SQUARE_SCORES[i] * COLOR_SCORES[$2] };
setSquare = function /* (state:$0, x:$1, y:$2) */ {
	i = $2 * 8 + $1;
	$0{:i} # $0{65} # $0{i + 1:65 - i - 1} # $0{65} # ';' # ($0{67:} + SQUARE_SCORES[i] * (COLOR_SCORES[$0{65}] - COLOR_SCORES[$0{i}]));
};
flipTurnColor = function /* (state:$0) */ { $0{:65} # OPPOSITE_COLORS[getTurnColor($0)] # $0{66:}; };

display = function /* (state) */ {
	print("\n" # getTurnColor($0) # "'s turn (score: " # getScore($0) # ")\n");
	for (y = 7; y >= 0; --y) {
		s = (y + 1) # ' | ';
		for (x = 0; x < 8; ++x) s #= $0{y * 8 + x} # ' ';
		print(s);
	};
	print("  +----------------\n    A B C D E F G H\n");
};

flip = function /* (state:$0, x:$1, y:$2, dx:$3, dy:$4) */ {
	original = $0;
	for ({ fc = OPPOSITE_COLORS[getTurnColor($0)]; c = void; }
			; { $1 += $3; $2 += $4; (0 <= $1 && $1 < 8 && 0 <= $2 && $2 < 8) && (c = $0{$2 * 8 + $1}) == fc }
			; $0 = setSquare($0, $1, $2));
	if (c != getTurnColor($0)) original else $0
};

place = function /* (state:$0, x:$1, y:$2) */ {
	if (getSquare($0, $1, $2) == '.') {
		original = $0;
		$0 = flip($0, $1, $2,  1,  0);
		$0 = flip($0, $1, $2,  1,  1);
		$0 = flip($0, $1, $2,  0,  1);
		$0 = flip($0, $1, $2, -1,  1);
		$0 = flip($0, $1, $2, -1,  0);
		$0 = flip($0, $1, $2, -1, -1);
		$0 = flip($0, $1, $2,  0, -1);
		$0 = flip($0, $1, $2,  1, -1);
		if ($0 != original) $0 = flipTurnColor(setSquare($0, $1, $2));
	};
	( $0 )
};

tryAll = function /* (state:$0, exec:$1) */ {
	for ({gotMove = false; i = 0}; i < 64
			&& ((newState = place($0, i % 8, i \ 8)) == $0 || { gotMove = true; $1(newState) }); ++i);
	if (!gotMove) $1(flipTurnColor($0));
	( gotMove )
};

alphaBeta = function /* (state:$0, depth:$1, alpha:$2, beta:$3) */ {
	if ($1 == 0) getScore($0)
	else {
		depth = $1; alpha = $2; beta = $3;
		tryAll($0, >{ alpha = max(alpha, -alphaBeta($0, depth - 1, -beta, -alpha)); (alpha < beta) });
		alpha
	}
};

reversiHelp = function {
	print("\nEnter moves like this: D2.\nType 'exit' to quit.\nType 'restart' to restart.\n"
			# "Type 'play' to have the computer play your color instead.\nType 'undo' to take back last move.\n");
};

init => { state = INIT_STATE; history.n = 0; display(state) };
/*
				n = 9;
				for (y = 0; y < 4; ++y) for (x = y; x < 4; ++x) {
					SQUARES_MAP[y][x]
					= SQUARES_MAP[y][7 - x]
					= SQUARES_MAP[7 - y][x]
					= SQUARES_MAP[7 - y][7 - x]
					= SQUARES_MAP[x][y]
					= SQUARES_MAP[x][7 - y]
					= SQUARES_MAP[7 - x][y]
					= SQUARES_MAP[7 - x][7 - y] = n;
					--n;
				};
				compose(@::SCORES_X, 13.369822692896, 155.9628038805, 14.88084246469, 12.101569299573, 23.376951181019, 10.452980127244, 287.77716394189, 18.539272695859, 18.982741796573, 1168.251138291);
				for (y = 0; y < 8; ++y) for (x = 0; x < 8; ++x) SQUARE_SCORES[y][x] = round(SCORES_X[SQUARES_MAP[y][x]]/10);
				for (y = 0; y < 8; ++y) { for ({s = ''; x = 0}; x < 8; ++x) s #= ', ' # repeat(' ', 3 - length(SQUARE_SCORES[y][x])) # SQUARE_SCORES[y][x]; print(s) };
*/
init();

for (continue = true; {
	history[history.n] = state;
	ok = false;
	userHadMoves = tryAll(state, >true);
	if (!userHadMoves) {
		print('No valid move for you.');
		state = flipTurnColor(state);
		ok = true;
	} else {
		s = replace(upper(input("> ")), "-", "");
		if (s === "EXIT") continue = false
		else if (s === "UNDO") { if (history.n > 0) state = history[--history.n]; display(state) }
		else if (s === "RESTART") init()
		else {
			if (s === "PLAY") ok = true
			else if (length(s) != 2) reversiHelp()
			else {
				x = ordinal(s{0}) - ordinal('A');
				y = ordinal(s{1}) - ordinal('1');
				if (!onBoard(x, y)) reversiHelp()
				else {
					newState = place(state, x, y);
					if (newState == state) print(s{0:2} # " is not a legal move")
					else {
						state = newState;
						ok = true;
					}
				};
			};
		}
	};
	if (ok) {
		display(state);
		history.n++;
		maxi = -infinity;
		print('Thinking...');
				
		computerHadMoves = tryAll(state, >{
			score = -alphaBeta($0, 5, -infinity, -maxi);
			// display($0);
			// print('   score: ' # score);
			if (score > maxi) { maxi = score; state = $0 };
			true
		});
		if (!computerHadMoves) print('No valid move for me.');
		display(state);
		if (!userHadMoves && !computerHadMoves) {
			for ({s = 0.0; i = 0}; i < 64; ++i) s += COLOR_SCORES[state{i}];
			print(if (s < 0) 'O won.' else if (s > 0) 'X won.' else 'Tie.');
			init();
			continue = false;
		};
	};
	( continue )
}; ) ;

