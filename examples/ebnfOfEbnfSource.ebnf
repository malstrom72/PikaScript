_ = { " " | "\t" | "\n" | "\r" } .
definition = term { _ [ ',' _ ] term } .
dquote = '"' { /[^\"]/ | '\' /?/ } '"' .
dressed = { /[^<>]/ } .
expr = _ orExpr { _ '...' _ orExpr } _ .
orExpr = definition { _ '|' _ definition } .
factor = meta | literal | '[' optExpr ']' | '{' repExpr '}' | '(' groupExpr ')' | '/' regExpr '/' .
groupExpr = expr .
literal = dquote | squote .
naked = symChar { symChar } .
optExpr = expr .
regExpr = { /[^\\\/]/ | '\' /?/ } .
repExpr = expr .
root = _ rule { _ rule } _ .
rule = symbol _ ( ':' | '=' ) expr ( '.' | ';' ) .
squote = /'*'/ .
symChar = /[a-zA-Z0-9$_]/ .
meta = symbol .
symbol = '<' dressed '>' | naked .
term = factor [ _ '-' _ factor ] .
